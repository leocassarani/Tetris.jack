class J {
    function void show(Tetromino tetromino, Board board, boolean fill) {
        var int col, row, rotation;

        let col = tetromino.getCol();
        let row = tetromino.getRow();
        let rotation = tetromino.getRotation();

        do board.setFill(fill);

        if (rotation = 0) {
            // ■
            // ■■■
            do board.fill(col, row);
            do board.fill(col, row - 1);
            do board.fill(col + 1, row - 1);
            do board.fill(col + 2, row - 1);
        } else {
            if (rotation = 1) {
                // ■■
                // ■
                // ■
                do board.fill(col, row);
                do board.fill(col + 1, row);
                do board.fill(col, row - 1);
                do board.fill(col, row - 2);
            } else {
                if (rotation = 2) {
                    // ■■■
                    //   ■
                    do board.fill(col, row);
                    do board.fill(col + 1, row);
                    do board.fill(col + 2, row);
                    do board.fill(col + 2, row - 1);
                } else {
                    //  ■
                    //  ■
                    // ■■
                    do board.fill(col + 1, row);
                    do board.fill(col + 1, row - 1);
                    do board.fill(col + 1, row - 2);
                    do board.fill(col, row - 2);
                }
            }
        }

        return;
    }

    // direction: 0 = down, 1 = left, 2 = right
    function boolean move(Tetromino tetromino, Board board, int direction) {
        var int col, row, rotation;

        let col = tetromino.getCol();
        let row = tetromino.getRow();
        let rotation = tetromino.getRotation();

        if (direction = 0) {
            // DOWN
            if (row < J.height(rotation)) {
                return false;
            }

            if (rotation = 0) {
                if (
                    board.isFull(col, row - 2)     |
                    board.isFull(col + 1, row - 2) |
                    board.isFull(col + 2, row - 2)
                ) {
                    return false;
                }
            } else {
                if (rotation = 1) {
                    if (
                        board.isFull(col + 1, row - 1) |
                        board.isFull(col, row - 3)
                    ) {
                        return false;
                    }
                } else {
                    if (rotation = 2) {
                        if (
                            board.isFull(col, row - 1) |
                            board.isFull(col + 2, row - 2)
                        ) {
                            return false;
                        }
                    } else {
                        if (
                            board.isFull(col, row - 3) |
                            board.isFull(col + 1, row - 3)
                        ) {
                            return false;
                        }
                    }
                }
            }

            do J.show(tetromino, board, false);
            do tetromino.setRow(row - 1);
        } else {
            if (direction = 1) {
                // LEFT
                if (col = 0) {
                    return false;
                }

                if (rotation = 0) {
                    if (
                        board.isFull(col - 1, row) |
                        board.isFull(col - 1, row - 1)
                    ) {
                        return false;
                    }
                } else {
                    if (rotation = 1) {
                        if (
                            board.isFull(col - 1, row)     |
                            board.isFull(col - 1, row - 1) |
                            board.isFull(col - 1, row - 2)
                        ) {
                            return false;
                        }
                    } else {
                        if (rotation = 2) {
                            if (
                                board.isFull(col - 1, row) |
                                board.isFull(col + 1, row - 1)
                            ) {
                                return false;
                            }
                        } else {
                            if (
                                board.isFull(col, row)     |
                                board.isFull(col, row - 1) |
                                board.isFull(col - 1, row - 2)
                            ) {
                                return false;
                            }
                        }
                    }
                }

                do J.show(tetromino, board, false);
                do tetromino.setCol(col - 1);
            } else {
                // RIGHT
                if ((col + J.width(rotation)) > 9) {
                    return false;
                }

                if (rotation = 0) {
                    if (
                        board.isFull(col + 1, row) |
                        board.isFull(col + 3, row - 1)
                    ) {
                        return false;
                    }
                } else {
                    if (rotation = 1) {
                        if (
                            board.isFull(col + 2, row)     |
                            board.isFull(col + 1, row - 1) |
                            board.isFull(col + 1, row - 2)
                        ) {
                            return false;
                        }
                    } else {
                        if (rotation = 2) {
                            if (
                                board.isFull(col + 3, row) |
                                board.isFull(col + 3, row - 1)
                            ) {
                                return false;
                            }
                        } else {
                            if (
                                board.isFull(col + 2, row)     |
                                board.isFull(col + 2, row - 1) |
                                board.isFull(col + 2, row - 2)
                            ) {
                                return false;
                            }
                        }
                    }
                }

                do J.show(tetromino, board, false);
                do tetromino.setCol(col + 1);
            }
        }

        do J.show(tetromino, board, true);
        return true;
    }

    function void rotate(Tetromino tetromino, Board board) {
        var int col, row, rotation;

        let col = tetromino.getCol();
        let row = tetromino.getRow();
        let rotation = tetromino.getRotation();

        if (rotation = 0) {
            if ((row - 1) < J.height(rotation)) {
                return;
            }

            if (
                board.isFull(col + 1, row) |
                board.isFull(col + 2, row) |
                board.isFull(col + 1, row - 2)
            ) {
                return;
            }

            do J.show(tetromino, board, false);
            do tetromino.setCol(col + 1);
            do tetromino.setRotation(1);
        } else {
            if (rotation = 1) {
                if (col = 0) {
                    return;
                }

                if (
                    board.isFull(col - 1, row - 1) |
                    board.isFull(col + 1, row - 1) |
                    board.isFull(col + 1, row - 2)
                ) {
                    return;
                }

                do J.show(tetromino, board, false);
                do tetromino.setCol(col - 1);
                do tetromino.setRow(row - 1);
                do tetromino.setRotation(2);
            } else {
                if (rotation = 2) {
                    if (
                        board.isFull(col + 1, row + 1) |
                        board.isFull(col + 1, row - 1) |
                        board.isFull(col, row - 1)
                    ) {
                        return;
                    }

                    do J.show(tetromino, board, false);
                    do tetromino.setRow(row + 1);
                    do tetromino.setRotation(3);
                } else {
                    if ((col + J.width(rotation)) > 9) {
                        return;
                    }

                    if (
                        board.isFull(col, row)     |
                        board.isFull(col, row - 1) |
                        board.isFull(col + 2, row - 1)
                    ) {
                        return;
                    }

                    do J.show(tetromino, board, false);
                    do tetromino.setRotation(0);
                }
            }
        }

        do J.show(tetromino, board, true);
        return;
    }

    function int width(int rotation) {
        if ((rotation = 0) | (rotation = 2)) {
            return 3;
        } else {
            return 2;
        }
    }

    function int height(int rotation) {
        if ((rotation = 0) | (rotation = 2)) {
            return 2;
        } else {
            return 3;
        }
    }
}
