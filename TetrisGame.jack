class TetrisGame {
    field Board board;
    field Tetromino tetromino;
    field int type, cleared;

    constructor TetrisGame new() {
        do Screen.clearScreen();

        // (196, 1)         (316, 1)
        // +-----------------------+
        // |                       |
        // |                       |
        // +-----------------------+
        // (196, 254)     (316, 254)

        let board = Board.new(196, 2, 21, 12);
        do board.drawOutline();

        let tetromino = Tetromino.new(0, 3, 19, board);
        do tetromino.show();

        let type = 0;
        let cleared = 0;

        do printScore();

        return this;
    }

    method void dispose() {
        do tetromino.dispose();
        do board.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var char key;
        var int waited;

        while (true) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
                do Sys.wait(100);

                let waited = waited + 1;
                if (waited = 5) {
                    do step();
                    let waited = 0;
                }
            }

            while (~(key = 0)) {
                if (key = 130) { // left
                    do tetromino.moveLeft();
                } else {
                    if (key = 132) { // right
                        do tetromino.moveRight();
                    } else {
                        if (key = 131) { // up
                            do tetromino.rotate();
                        } else {
                            if (key = 133) { // down
                                do step();
                            }
                        }
                    }
                }

                do Sys.wait(100);

                let waited = waited + 1;
                if (waited = 5) {
                    do step();
                    let waited = 0;
                }

                let key = Keyboard.keyPressed();
            }
        }

        return;
    }

    method void step() {
        if (tetromino.step()) {
            return;
        }

        let cleared = cleared + board.clearFullLines();
        do printScore();

        let type = type + 1;
        if (type > 6) {
            let type = 0;
        }

        do tetromino.reset(type, 3, 20);
        do tetromino.show();

        return;
    }

    method void printScore() {
        do Output.moveCursor(4, 8);
        do Output.printString("Score: ");
        do Output.printInt(0);

        do Output.moveCursor(11, 8);
        do Output.printString("Level: ");
        do Output.printInt(0);

        do Output.moveCursor(18, 8);
        do Output.printString("Lines: ");
        do Output.printInt(cleared);

        return;
    }
}
