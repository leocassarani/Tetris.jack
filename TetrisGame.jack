class TetrisGame {
    field Board board;
    field Tetromino tetromino;

    constructor TetrisGame new() {
        do Screen.clearScreen();

        // (196, 1)         (316, 1)
        // +-----------------------+
        // |                       |
        // |                       |
        // +-----------------------+
        // (196, 254)     (316, 254)

        let board = Board.new(196, 2, 21, 12);
        do board.drawOutline();

        let tetromino = Tetromino.new(0, 3, 19, board);
        do tetromino.show();

        return this;
    }

    method void dispose() {
        do tetromino.dispose();
        do board.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var char key;
        var int waited, type;
        var boolean ok;

        while (true) {
            let ok = false;

            while (key = 0) {
                let key = Keyboard.keyPressed();
                do Sys.wait(100);

                let waited = waited + 1;
                if (waited = 10) {
                    //do tetromino.step();
                    let waited = 0;
                }
            }

            while (~(key = 0)) {
                if (key = 130) { // left
                    do tetromino.moveLeft();
                } else {
                    if (key = 132) { // right
                        do tetromino.moveRight();
                    } else {
                        if (key = 131) { // up
                            do tetromino.rotate();
                        } else {
                            if (key = 133) { // down
                                let ok = tetromino.step();
                            } else {
                                if (key = 128) { // enter
                                    let type = type + 1;
                                    if (type > 6) {
                                        let type = 0;
                                    }
                                    do tetromino.reset(type, 3, 20);
                                    do tetromino.show();
                                }
                            }
                        }
                    }
                }

                if (~(ok)) {
                    do board.clearFullLines();
                }

                do Sys.wait(100);

                let waited = waited + 1;
                if (waited = 10) {
                    //do tetromino.step();
                    let waited = 0;
                }

                let key = Keyboard.keyPressed();
            }
        }

        return;
    }
}
