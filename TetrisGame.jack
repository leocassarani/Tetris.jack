class TetrisGame {
    field Tetromino tetromino;

    constructor TetrisGame new() {
        do Screen.clearScreen();

        // (196, 1)         (316, 1)
        // +-----------------------+
        // |                       |
        // |                       |
        // +-----------------------+
        // (196, 254)     (316, 254)

        do Screen.drawLine(195, 1, 317, 1);
        do Screen.drawLine(195, 1, 195, 254);
        do Screen.drawLine(317, 1, 317, 254);
        do Screen.drawLine(195, 254, 317, 254);

        let tetromino = Tetromino.new(232, 2);
        do tetromino.draw();

        return this;
    }

    method void dispose() {
        do tetromino.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var char key;
        var int waited;

        while (true) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
                do Sys.wait(50);

                let waited = waited + 1;
                if (waited = 10) {
                    do tetromino.step();
                    let waited = 0;
                }
            }

            while (~(key = 0)) {
                if (key = 130) {
                    do tetromino.moveLeft();
                } else {
                    if (key = 132) {
                        do tetromino.moveRight();
                    }
                }

                do Sys.wait(50);

                let waited = waited + 1;
                if (waited = 10) {
                    do tetromino.step();
                    let waited = 0;
                }

                let key = Keyboard.keyPressed();
            }
        }

        return;
    }
}
