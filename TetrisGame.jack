class TetrisGame {
    field Board board;
    field Tetromino tetromino;
    field int type, cleared, level, difficulty;

    constructor TetrisGame new() {
        do Screen.clearScreen();

        // (196, 1)         (316, 1)
        // +-----------------------+
        // |                       |
        // |                       |
        // +-----------------------+
        // (196, 254)     (316, 254)

        let board = Board.new(196, 2, 21, 12);
        do board.drawOutline();

        let tetromino = Tetromino.new(0, 3, 19, board);
        do tetromino.show();

        let type = 0;
        let cleared = 0;
        let level = 0;
        let difficulty = 12;

        do Output.moveCursor(11, 47);
        do Output.printString("Next:");

        do printScore();
        do showNextTetromino();

        return this;
    }

    method void dispose() {
        do tetromino.dispose();
        do board.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var char key;
        var int waited;

        let waited = difficulty;

        while (true) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
                do Sys.wait(100);

                let waited = waited - 1;
                if (waited = 0) {
                    do step();
                    let waited = difficulty;
                }
            }

            while (~(key = 0)) {
                if (key = 130) { // left
                    do tetromino.moveLeft();
                } else {
                    if (key = 132) { // right
                        do tetromino.moveRight();
                    } else {
                        if (key = 131) { // up
                            do tetromino.rotate();
                        } else {
                            if (key = 133) { // down
                                do step();
                            }
                        }
                    }
                }

                do Sys.wait(100);

                let waited = waited - 1;
                if (waited = 0) {
                    do step();
                    let waited = difficulty;
                }

                let key = Keyboard.keyPressed();
            }
        }

        return;
    }

    method void step() {
        var int lines;

        if (tetromino.step()) {
            return;
        }

        let lines = board.clearFullLines();

        if (~(lines = 0)) {
            let cleared = cleared + lines;

            if (cleared < 100) {
                let level = cleared / 10;
            } else {
                let level = 10;
            }

            let difficulty = 12 - level;

            do printScore();
        }

        let type = type + 1;
        if (type > 6) {
            let type = 0;
        }

        do tetromino.reset(type, 3, 20);
        do tetromino.show();

        do showNextTetromino();

        return;
    }

    method void printScore() {
        do Output.moveCursor(7, 8);
        do Output.printString("Level: ");
        do Output.printInt(level);

        do Output.moveCursor(15, 8);
        do Output.printString("Lines: ");
        do Output.printInt(cleared);

        return;
    }

    method void showNextTetromino() {
        var int next, size;
        var Array tiles;

        let next = type + 1;
        if (next > 6) {
            let next = 0;
        }

        let tiles = Tetromino.tiles(next, 0);
        let size = Tetromino.size(next);

        do Grid.drawPreview(tiles, size);

        return;
    }
}
