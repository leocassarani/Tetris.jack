class Board {
    field int minX, minY, rows, tile;
    field boolean fill;
    field Array tiles;

    constructor Board new(int x, int y, int rowsArg, int tileArg) {
        var int i, j;
        var Array row;

        let minX = x;
        let minY = y;
        let rows = rowsArg;
        let tile = tileArg;
        let fill = false;
        let tiles = Array.new(rows);

        while (i < rows) {
            let row = Array.new(10);

            while (j < 10) {
                let row[j] = false;
                let j = j + 1;
            }

            let tiles[i] = row;
            let i = i + 1;
        }

        return this;
    }

    method void dispose() {
        var int i;
        var Array row;

        while (i < rows) {
            let row = tiles[i];
            do row.dispose();
        }
        do tiles.dispose();

        do Memory.deAlloc(this);
        return;
    }

    method boolean isFull(int col, int row) {
        var Array tileRow;

        if (row > (rows - 1)) {
            return false;
        }

        let tileRow = tiles[row];
        return tileRow[col];
    }

    method void setFill(boolean fillArg) {
        let fill = fillArg;
        return;
    }

    method void fill(int col, int row) {
        var Array tileRow;

        if (row > (rows - 1)) {
            return;
        }

        let tileRow = tiles[row];
        let tileRow[col] = fill;

        do drawTile(col, row, fill);
        return;
    }

    method void drawTile(int col, int row, boolean fill) {
        var int x, y, parity;

        let x = minX + (tile * col);
        let y = minY + (tile * (rows - (row + 1)));
        let parity = col & 3;

        do Grid.drawTile(x, y, parity, fill);
        return;
    }

    method void drawOutline() {
        var int i;
        var Array screen;

        let screen = 16384;

        // Equivalent to: Screen.drawLine(195, 1, 195, 254)
        let i = 44;
        while (i < 8160) {
            let screen[i] = 8;
            let i = i + 32;
        }

        // Equivalent to: Screen.drawLine(317, 1, 317, 254)
        let i = 51;
        while (i < 8160) {
            let screen[i] = 8192;
            let i = i + 32;
        }

        return;
    }

    method void clearFullLines() {
        var int i, j;
        var boolean full;
        var Array row;

        while (i < rows) {
            let row = tiles[i];
            let full = true;
            let j = 0;

            while (j < 10) {
                let full = full & row[j];
                let j = j + 1;
            }

            if (full) {
                do shiftLines(i + 1);
            } else {
                let i = i + 1;
            }
        }

        return;
    }

    method void shiftLines(int i) {
        var int j;
        var Array src, dst;

        while (i < rows) {
            let src = tiles[i];
            let dst = tiles[i - 1];
            let j = 0;

            while (j < 10) {
                let dst[j] = src[j];
                let src[j] = false;
                do drawTile(j, i - 1, dst[j]);
                let j = j + 1;
            }

            let i = i + 1;
        }

        let j = 0;
        let i = i - 1;

        while (j < 10) {
            do drawTile(j, i, false);
            let j = j + 1;
        }

        return;
    }
}
