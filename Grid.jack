class Grid {
    static Array screen, draw;

    function void init() {
        let screen = 16384;

        let draw = Array.new(8);
        let draw[0] = -16;   // 0xFFF0
        let draw[2] = 4095;  // 0x0FFF
        let draw[4] = -4096; // 0xF000
        let draw[5] = 255;   // 0x00FF
        let draw[6] = -256;  // 0xFF00
        let draw[7] = 15;    // 0x000F

        return;
    }

    function void drawOutline() {
        var int i;

        // Equivalent to: Screen.drawLine(195, 2, 195, 254)
        let i = 76;
        while (i < 8128) {
            let screen[i] = 4;
            let i = i + 32;
        }

        // Equivalent to: Screen.drawLine(317, 2, 317, 254)
        let i = 83;
        while (i < 8128) {
            let screen[i] = 8192;
            let i = i + 32;
        }

        return;
    }

    function void drawTile(int x, int y, int parity, boolean fill) {
        var int i, max, wordOne, wordTwo;

        let i = (y * 32) + (x / 16);
        let max = i + 384; // i + (12 * 32)

        let parity = parity + parity;
        let wordOne = draw[parity];
        let wordTwo = draw[parity + 1];

        if (fill) {
            while (i < max) {
                let screen[i] = screen[i] | wordOne;
                let i = i + 1;
                let screen[i] = screen[i] | wordTwo;
                let i = i + 31;
            }
        } else {
            let wordOne = ~(wordOne);
            let wordTwo = ~(wordTwo);

            while (i < max) {
                let screen[i] = screen[i] & wordOne;
                let i = i + 1;
                let screen[i] = screen[i] & wordTwo;
                let i = i + 31;
            }
        }

        return;
    }

    function void drawPreview(Array tiles, int size) {
        var int col, row, i, x, y, parity, fill;

        let y = 106;

        while (row < 4) {
            let col = 0;
            let x = 420;

            while (col < 4) {
                let parity = col & 3;

                if ((row < size) & (col < size)) {
                    let fill = tiles[i];
                    let i = i + 1;
                } else {
                    let fill = false;
                }

                do Grid.drawTile(x, y, parity, fill);

                let col = col + 1;
                let x = x + 12;
            }

            let row = row + 1;
            let y = y + 12;
        }

        return;
    }
}
