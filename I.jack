class I {
    function void show(Tetromino tetromino, Board board, boolean fill) {
        var int col, row;

        let col = tetromino.getCol();
        let row = tetromino.getRow();

        do board.setFill(fill);

        if (tetromino.getRotation() = 0) {
            // ■■■■
            do board.fill(col, row);
            do board.fill(col + 1, row);
            do board.fill(col + 2, row);
            do board.fill(col + 3, row);
        } else {
            // ■
            // ■
            // ■
            // ■
            do board.fill(col, row);
            do board.fill(col, row - 1);
            do board.fill(col, row - 2);
            do board.fill(col, row - 3);
        }

        return;
    }

    // direction: 0 = down, 1 = left, 2 = right
    function void move(Tetromino tetromino, Board board, int direction) {
        var int col, row;

        let col = tetromino.getCol();
        let row = tetromino.getRow();

        if (direction = 0) {
            // DOWN
            if (tetromino.getRotation() = 0) {
                if (
                    (row = 0)                      |
                    board.isFull(col, row - 1)     |
                    board.isFull(col + 1, row - 1) |
                    board.isFull(col + 2, row - 1) |
                    board.isFull(col + 3, row - 1)
                ) {
                    return;
                }
            } else {
                if ((row < 4) | board.isFull(col, row - 4)) {
                    return;
                }
            }

            do I.show(tetromino, board, false);
            do tetromino.setRow(row - 1);
        } else {
            if (direction = 1) {
                // LEFT
                if (col = 0) {
                    return;
                }

                if (tetromino.getRotation() = 0) {
                    if (board.isFull(col - 1, row)) {
                        return;
                    }
                } else {
                    if (
                        board.isFull(col - 1, row)     |
                        board.isFull(col - 1, row - 1) |
                        board.isFull(col - 1, row - 2) |
                        board.isFull(col - 1, row - 3)
                    ) {
                        return;
                    }
                }

                do I.show(tetromino, board, false);
                do tetromino.setCol(col - 1);
            } else {
                // RIGHT
                if (tetromino.getRotation() = 0) {
                    if ((col > 5) | board.isFull(col + 4, row)) {
                        return;
                    }
                } else {
                    if (
                        (col = 9)                      |
                        board.isFull(col + 1, row)     |
                        board.isFull(col + 1, row - 1) |
                        board.isFull(col + 1, row - 2) |
                        board.isFull(col + 1, row - 3)
                    ) {
                        return;
                    }
                }

                do I.show(tetromino, board, false);
                do tetromino.setCol(col + 1);
            }
        }

        do I.show(tetromino, board, true);
        return;
    }

    function void rotate(Tetromino tetromino, Board board) {
        var int col, row;

        let col = tetromino.getCol();
        let row = tetromino.getRow();

        if (tetromino.getRotation() = 0) {
            if (
                (row < 1) |
                board.isFull(col + 2, row + 2) |
                board.isFull(col + 2, row + 1) |
                board.isFull(col + 2, row - 1)
            ) {
                return;
            }

            do I.show(tetromino, board, false);
            do tetromino.setRow(row + 2);
            do tetromino.setCol(col + 2);
            do tetromino.setRotation(1);
        } else {
            if (
                (col < 2) |
                (col = 9) |
                board.isFull(col - 2, row) |
                board.isFull(col - 1, row) |
                board.isFull(col + 2, row)
            ) {
                return;
            }

            do I.show(tetromino, board, false);
            do tetromino.setRow(row - 2);
            do tetromino.setCol(col - 2);
            do tetromino.setRotation(0);
        }

        do I.show(tetromino, board, true);
        return;
    }
}
