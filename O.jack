class O {
    function void show(Tetromino tetromino, Board board, boolean fill) {
        var int col, row;

        let col = tetromino.getCol();
        let row = tetromino.getRow();

        do board.setFill(fill);

        // ■■
        // ■■
        do board.fill(col, row);
        do board.fill(col + 1, row);
        do board.fill(col, row - 1);
        do board.fill(col + 1, row - 1);

        return;
    }

    // direction: 0 = down, 1 = left, 2 = right
    function boolean move(Tetromino tetromino, Board board, int direction) {
        var int col, row;

        let col = tetromino.getCol();
        let row = tetromino.getRow();

        if (direction = 0) {
            // DOWN
            if (
                (row < 2)                  |
                board.isFull(col, row - 2) |
                board.isFull(col + 1, row - 2)
            ) {
                return false;
            }

            do O.show(tetromino, board, false);
            do tetromino.setRow(row - 1);
        } else {
            if (direction = 1) {
                // LEFT
                if (
                    (col = 0)                  |
                    board.isFull(col - 1, row) |
                    board.isFull(col - 1, row - 1)
                ) {
                    return false;
                }

                do O.show(tetromino, board, false);
                do tetromino.setCol(col - 1);
            } else {
                // RIGHT
                if (
                    (col > 7)                  |
                    board.isFull(col + 2, row) |
                    board.isFull(col + 2, row - 1)
                ) {
                    return false;
                }

                do O.show(tetromino, board, false);
                do tetromino.setCol(col + 1);
            }
        }

        do O.show(tetromino, board, true);
        return true;
    }
}
