class Tetromino {
    field int type, col, row, rotation;
    field Board board;

    function void init() {
        do I.init();
        do J.init();
        do L.init();
        do O.init();
        return;
    }

    constructor Tetromino new(int typeArg, int colArg, int rowArg, Board boardArg) {
        let type = typeArg;
        let col = colArg;
        let row = rowArg;
        let board = boardArg;
        let rotation = 0;
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void reset(int typeArg, int colArg, int rowArg) {
        let type = typeArg;
        let col = colArg;
        let row = rowArg;
        let rotation = 0;
        return;
    }

    method void show(boolean fill) {
        var int x, y, i, size;
        var Array tiles;

        let tiles = _tiles();
        let size = _size();

        do board.setFill(fill);

        while (y < size) {
            let x = 0;

            while (x < size) {
                if (tiles[i]) {
                    do board.fill(col + x, row - y);
                }

                let x = x + 1;
                let i = i + 1;
            }

            let y = y + 1;
        }

        return;
    }

    method boolean step() {
        var int x, y, i, size, max, tile;
        var Array tiles;

        let tiles = _tiles();
        let size = _size();

        let max = size - 1;

        while (y < size) {
            let x = 0;

            while (x < size) {
                let tile = tiles[i];

                if (y < max) {
                    if (tiles[i] & tiles[i + size]) {
                        let tile = false;
                    }
                }

                if (tile) {
                    if (board.isFull(col + x, (row - y) - 1)) {
                        return false;
                    }
                }

                let x = x + 1;
                let i = i + 1;
            }

            let y = y + 1;
        }

        do show(false);
        let row = row - 1;
        do show(true);

        return true;
    }

    method void moveLeft() {
        var int x, y, i, size, tile;
        var Array tiles;

        let tiles = _tiles();
        let size = _size();

        while (y < size) {
            let x = 0;

            while (x < size) {
                let tile = tiles[i];

                if (x > 0) {
                    if (tiles[i] & tiles[i - 1]) {
                        let tile = false;
                    }
                }

                if (tile) {
                    if (board.isFull((col + x) - 1, row - y)) {
                        return;
                    }
                }

                let x = x + 1;
                let i = i + 1;
            }

            let y = y + 1;
        }

        do show(false);
        let col = col - 1;
        do show(true);

        return;
    }

    method void moveRight() {
        var int x, y, i, size, max, tile;
        var Array tiles;

        let tiles = _tiles();
        let size = _size();

        let max = size - 1;

        while (y < size) {
            let x = 0;

            while (x < size) {
                let tile = tiles[i];

                if (x < max) {
                    if (tiles[i] & tiles[i + 1]) {
                        let tile = false;
                    }
                }

                if (tile) {
                    if (board.isFull((col + x) + 1, row - y)) {
                        return;
                    }
                }

                let x = x + 1;
                let i = i + 1;
            }

            let y = y + 1;
        }

        do show(false);
        let col = col + 1;
        do show(true);

        return;
    }

    method void rotate() {
        do show(false);

        let rotation = rotation + 1;
        if (rotation > 3) {
            let rotation = 0;
        }

        do show(true);
        return;
    }

    method Array _tiles() {
        if (type = 0) {
            return I.tiles(rotation);
        } else {
            if (type = 1) {
                return J.tiles(rotation);
            } else {
                if (type = 2) {
                    return L.tiles(rotation);
                } else {
                    return O.tiles(rotation);
                }
            }
        }
    }

    method int _size() {
        if (type = 0) {
            return I.size();
        } else {
            if (type = 1) {
                return L.size();
            } else {
                if (type = 2) {
                    return L.size();
                } else {
                    return O.size();
                }
            }
        }
    }
}
